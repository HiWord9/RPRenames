plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/${mod_id}.accesswidener")

	mixin {
		defaultRefmapName = "${mod_id}-refmap.json"
	}
}

repositories {
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases/" }
}

dependencies {
	// Basic settings
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
	modImplementation "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"

	modImplementation "io.github.llamalad7:mixinextras-fabric:0.2.2"
}

processResources {

	def expandProps = [
			"version"                         : version,
			"group"                           : project.group, //Else we target the task's group.
			"mod_main_dir"                    : mod_main_dir,
			"mod_client_class"                : mod_client_class,
			"mod_modmenu_impl_subdir"         : mod_modmenu_impl_subdir,

			"fabric_version"                  : fabric_version,
			"loader_version"                  : loader_version,

			"mod_id"                          : mod_id,
			"mod_name"                        : mod_name,
			"mod_description"                 : mod_description,
			"mod_author"                      : mod_author,

			"mod_homepage_modrinth"           : mod_homepage_modrinth,
			"mod_homepage_curseforge"         : mod_homepage_curseforge,
			"mod_donate_paypal"               : mod_donate_paypal,

			"mod_sources"                     : mod_sources,
			"mod_issue_tracker"               : mod_issue_tracker,
			"mod_license_link"                : mod_license_link,
			"mod_license"                     : mod_license,
			"mod_license_version"             : mod_license_version,

			"minecraft_version_range"         : minecraft_version_range,
			"loader_version_range"            : loader_version_range,
			"cloth_config_version_range"      : cloth_config_version_range,
			"java_version"                    : java_version
	]
	filesMatching(['fabric.mod.json', mod_id + '.mixins.json']) {
		expand expandProps
	}
	inputs.properties(expandProps)
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = Integer.valueOf(java_version)
}

java {
	// generating sources
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
// (currently not used)
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
		// Add repositories to publish to here.
	}
}
